cmake_minimum_required(VERSION 3.5 FATAL_ERROR)

project(boost-externalproj LANGUAGES CXX)

set(CMAKE_CXX_STANDARD 11)
set(CMAKE_CXX_EXTENSIONS OFF)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

set_property(DIRECTORY PROPERTY EP_BASE ${CMAKE_BINARY_DIR}/subprojects)

set(STAGED_INSTALL_PREFIX ${CMAKE_BINARY_DIR}/stage)
message(STATUS "${PROJECT_NAME} staged install: ${STAGED_INSTALL_PREFIX}")


add_subdirectory(cmake)

include(ExternalProject)
ExternalProject_Add(${PROJECT_NAME}
  DEPENDS
    boost double-conversion fmt gflags glog libevent sodium 
  SOURCE_DIR
    ${CMAKE_CURRENT_LIST_DIR}/src
  CMAKE_ARGS
    -DCMAKE_CXX_COMPILER=${CMAKE_CXX_COMPILER}
    -DCMAKE_CXX_STANDARD=${CMAKE_CXX_STANDARD}
    -DCMAKE_CXX_EXTENSIONS=${CMAKE_CXX_EXTENSIONS}
    -DCMAKE_CXX_STANDARD_REQUIRED=${CMAKE_CXX_STANDARD_REQUIRED}
  CMAKE_CACHE_ARGS
    -DCMAKE_CXX_FLAGS:STRING=${CMAKE_CXX_FLAGS}
    -DCMAKE_INCLUDE_PATH:PATH=${Boost_INCLUDEDIR}:${DOUBLE_CONVERSION_INCLUDEDIR}:${FMT_INCLUDEDIR}:${GFLAGS_INCLUDEDIR}:${GLOG_INCLUDEDIR}:${LIBEVENT_INCLUDEDIR}:${SODIUM_INCLUDEDIR}
    -DCMAKE_LIBRARY_PATH:PATH=${Boost_LIBRARYDIR}:${DOUBLE_CONVERSION_LIBRARYDIR}:${FMT_LIBRARYDIR}:${GFLAGS_LIBRARYDIR}:${GLOG_LIBRARYDIR}:${LIBEVENT_LIBRARYDIR}:${SODIUM_LIBRARYDIR}
  BUILD_ALWAYS
    1
  INSTALL_COMMAND
    ""
  )